{-# OPTIONS --safe #-}
module CNF where

open import Prelude
open import Meta.Effect
open import Meta.Effect.Map.Lawful

open import Data.Empty hiding (_â‰ _)
open import Data.Dec as Dec
open import Data.Bool as Bool
open import Data.Fin as Fin
open import Data.Fin.Operations
open import Data.List as List
open import Data.List.Operations.Properties

open import Proposition

-- conjunctive normal forms
-- https://1lab.dev/Logic.Propositional.Classical.CNF.html

data Lit (n : â„•) : Type where
  pos : Fin n â†’ Lit n
  neg : Fin n â†’ Lit n

-- TODO try LFSet

Clause : (n : â„•) â†’ ğ’°
Clause n = List (Lit n)

CNF : (n : â„•) â†’ ğ’°
CNF n = List (Clause n)

private variable
  n : â„•
  Î³ : Ctx n
  p : Prp n

posâ‰ neg : âˆ€ {x y : Fin n} â†’ pos x ï¼ neg y â†’ âŠ¥
posâ‰ neg {n = n} p = subst is-pos p tt where
  is-pos : Lit n â†’ Type
  is-pos (pos x) = âŠ¤
  is-pos (neg x) = âŠ¥

lit-var : Lit n â†’ Fin n
lit-var (pos x) = x
lit-var (neg x) = x

lit-val : Lit n â†’ Bool
lit-val (pos _) = true
lit-val (neg _) = false

pos-inj : {x y : Fin n} â†’ pos x ï¼ pos y â†’ x ï¼ y
pos-inj p = ap lit-var p

neg-inj : {x y : Fin n} â†’ neg x ï¼ neg y â†’ x ï¼ y
neg-inj p = ap lit-var p

instance
  Discrete-Lit : is-discrete (Lit n)
  Discrete-Lit {x = pos x} {y = pos y} = Dec.dmap (ap pos) (contra pos-inj) (x â‰Ÿ y)
  Discrete-Lit {x = pos x} {y = neg y} = no posâ‰ neg
  Discrete-Lit {x = neg x} {y = pos y} = no (posâ‰ neg âˆ˜ sym)
  Discrete-Lit {x = neg x} {y = neg y} = Dec.dmap (ap neg) (contra neg-inj) (x â‰Ÿ y)

avoid-lit : (i : Fin (suc n)) (x : Lit (suc n)) â†’ i â‰  lit-var x â†’ Lit n
avoid-lit i (pos x) p = pos (avoid i x p)
avoid-lit i (neg x) p = neg (avoid i x p)

-- semantics

pos-sem : Lit n â†’ Val n â†’ Bool
pos-sem (pos x) v = v x
pos-sem (neg x) v = not (v x)

clause-sem : Clause n â†’ Val n â†’ Bool
clause-sem ps v = any (Î» a â†’ pos-sem a v) ps

cnf-sem : CNF n â†’ Val n â†’ Bool
cnf-sem ps v = all (Î» d â†’ clause-sem d v) ps

instance
  âŸ¦âŸ§-Lit : âŸ¦âŸ§-notation (Lit n)
  âŸ¦âŸ§-Lit {n} = brackets (Val n â†’ Bool) pos-sem

  âŸ¦âŸ§-Clause : âŸ¦âŸ§-notation (Clause n)
  âŸ¦âŸ§-Clause {n} = brackets (Val n â†’ Bool) clause-sem

  âŸ¦âŸ§-CNF : âŸ¦âŸ§-notation (CNF n)
  âŸ¦âŸ§-CNF {n} = brackets (Val n â†’ Bool) cnf-sem

-- soundness

cnf-atom : Fin n â†’ CNF n
cnf-atom x = (pos x âˆ· []) âˆ· []

âŠ¤cnf : CNF n
âŠ¤cnf = []

âŠ¥cnf : CNF n
âŠ¥cnf = [] âˆ· []

_âˆ§cnf_ : CNF n â†’ CNF n â†’ CNF n
xs âˆ§cnf ys = xs List.++ ys

cnf-distrib : Clause n â†’ CNF n â†’ CNF n
cnf-distrib p q = map (p List.++_) q

_âˆ¨cnf_ : CNF n â†’ CNF n â†’ CNF n
[]       âˆ¨cnf q = []
(p âˆ· ps) âˆ¨cnf q = cnf-distrib p q âˆ§cnf (ps âˆ¨cnf q)

Â¬lit : Lit n â†’ Lit n
Â¬lit (pos x) = neg x
Â¬lit (neg x) = pos x

Â¬clause : Clause n â†’ CNF n
Â¬clause = map (Î» a â†’ Â¬lit a âˆ· [])

Â¬cnf : CNF n â†’ CNF n
Â¬cnf []      = âŠ¥cnf
Â¬cnf (p âˆ· q) = Â¬clause p âˆ¨cnf Â¬cnf q

literal-eq-negate : âˆ€ (x y : Lit n) â†’ x â‰  y â†’ lit-var x ï¼ lit-var y â†’ x ï¼ Â¬lit y
literal-eq-negate (pos x) (pos y) xâ‰ y p = absurd (xâ‰ y (ap pos p))
literal-eq-negate (pos x) (neg y) xâ‰ y p = ap pos p
literal-eq-negate (neg x) (pos y) xâ‰ y p = ap neg p
literal-eq-negate (neg x) (neg y) xâ‰ y p = absurd (xâ‰ y (ap neg p))

literal-sat-val : âˆ€ (x : Lit n) â†’ (v : Val n) â†’ âŒ âŸ¦ x âŸ§ v âŒŸ â†’ v (lit-var x) ï¼ lit-val x
literal-sat-val (pos x) v xt = soâ‰ƒis-true $ xt
literal-sat-val (neg x) v xt = Â¬soâ‰ƒis-false $ so-not xt

cnf-atom-sound : (x : Fin n) (v : Val n) â†’ âŸ¦ cnf-atom x âŸ§ v ï¼ v x
cnf-atom-sound x v = and-id-r _ âˆ™ or-id-r _

âŠ¥cnf-sound : (v : Val n) â†’ âŒ not (âŸ¦ âŠ¥cnf âŸ§ v) âŒŸ
âŠ¥cnf-sound v = oh

âŠ¤cnf-sound : (v : Val n) â†’ âŒ âŸ¦ âŠ¤cnf âŸ§ v âŒŸ
âŠ¤cnf-sound v = oh

âˆ§cnf-sound : (Ï• Ïˆ : CNF n) â†’ (v : Val n)
           â†’ âŸ¦ Ï• âˆ§cnf Ïˆ âŸ§ v ï¼ (âŸ¦ Ï• âŸ§ v) and (âŸ¦ Ïˆ âŸ§ v)
âˆ§cnf-sound Ï• Ïˆ v =
  all?-++ {p = Î» d â†’ clause-sem d v} {xs = Ï•} {ys = Ïˆ}

cnf-distrib-sound : (Ï• : Clause n) (Ïˆ : CNF n)
                  â†’ (v : Val n)
                  â†’ âŸ¦ cnf-distrib Ï• Ïˆ âŸ§ v ï¼ (âŸ¦ Ï• âŸ§ v) or (âŸ¦ Ïˆ âŸ§ v)
cnf-distrib-sound  []     Ïˆ v =
  ap (all (Î» d â†’ clause-sem d v)) (happly map-pres-id Ïˆ)
cnf-distrib-sound (p âˆ· Ï•) Ïˆ v =
  all (Î» d â†’ âŸ¦ d âŸ§ v) (map (Î» ys â†’ p âˆ· (Ï• List.++ ys)) Ïˆ)   ~âŸ¨ all?-map {xs = Ïˆ} âŸ©
  all (Î» d â†’ âŸ¦ p âˆ· (Ï• List.++ d) âŸ§ v) Ïˆ                     ~âŸ¨ ap (Î» d â†’ all d Ïˆ) (fun-ext Î» d â†’ any?-++ {xs = p âˆ· Ï•}) âŸ©
  all (Î» d â†’ (âŸ¦ p âˆ· Ï• âŸ§ v) or (âŸ¦ d âŸ§ v)) Ïˆ                  ~âŸ¨ all?-or {xs = Ïˆ} âŸ©
  (âŸ¦ p âˆ· Ï• âŸ§ v) or (âŸ¦ Ïˆ âŸ§ v)                                âˆ

âˆ¨cnf-sound : (Ï• Ïˆ : CNF n)
           â†’ (v : Val n)
           â†’ âŸ¦ Ï• âˆ¨cnf Ïˆ âŸ§ v ï¼ (âŸ¦ Ï• âŸ§ v) or (âŸ¦ Ïˆ âŸ§ v)
âˆ¨cnf-sound  []      Ïˆ v = refl
âˆ¨cnf-sound (p âˆ· Ï•) Ïˆ v =
  âŸ¦ (p âˆ· Ï•) âˆ¨cnf Ïˆ âŸ§ v                                     ~âŸ¨ all?-++ {xs = cnf-distrib p Ïˆ} âŸ©
  (âŸ¦ cnf-distrib p Ïˆ âŸ§ v) and (âŸ¦ Ï• âˆ¨cnf Ïˆ âŸ§ v)             ~âŸ¨ apÂ² _and_ (cnf-distrib-sound p Ïˆ v) (âˆ¨cnf-sound Ï• Ïˆ v) âŸ©
  ((âŸ¦ p âŸ§ v) or (âŸ¦ Ïˆ âŸ§ v)) and ((âŸ¦ Ï• âŸ§ v) or (âŸ¦ Ïˆ âŸ§ v))    ~âŸ¨ or-distrib-and-r (âŸ¦ p âŸ§ v) _ _ âŸ¨
  âŸ¦ p âˆ· Ï• âŸ§ v or âŸ¦ Ïˆ âŸ§ v                                   âˆ

Â¬lit-sound : (a : Lit n) (v : Val n)
           â†’ âŸ¦ Â¬lit a âŸ§ v ï¼ not (âŸ¦ a âŸ§ v)
Â¬lit-sound (pos x) v = refl
Â¬lit-sound (neg x) v = not-invol (v x) â»Â¹

Â¬clause-sound : (Ï• : Clause n) (v : Val n)
              â†’ âŸ¦ Â¬clause Ï• âŸ§ v ï¼ not (âŸ¦ Ï• âŸ§ v)
Â¬clause-sound Ï• v =
  all (Î» d â†’ âŸ¦ d âŸ§ v) (map (Î» a â†’ Â¬lit a âˆ· []) Ï•)  ~âŸ¨ all?-map {xs = Ï•} âŸ©
  all (Î» a â†’ (âŸ¦ Â¬lit a âŸ§ v) or false) Ï•            ~âŸ¨ ap (Î» a â†’ all a Ï•) (fun-ext Î» a â†’ or-id-r (âŸ¦ Â¬lit a âŸ§ v)) âŸ©
  all (Î» a â†’ âŸ¦ Â¬lit a âŸ§ v) Ï•                       ~âŸ¨ ap (Î» a â†’ all a Ï•) (fun-ext Î» a â†’ Â¬lit-sound a v) âŸ©
  all (Î» a â†’ not (âŸ¦ a âŸ§ v)) Ï•                      ~âŸ¨ not-any? {xs = Ï•} âŸ¨
  not (âŸ¦ Ï• âŸ§ v)                                    âˆ

Â¬cnf-sound : (Ï• : CNF n) (v : Val n)
           â†’ âŸ¦ Â¬cnf Ï• âŸ§ v ï¼ not (âŸ¦ Ï• âŸ§ v)
Â¬cnf-sound  []      v = refl
Â¬cnf-sound (p âˆ· Ï•) v =
  âŸ¦ (Â¬clause p âˆ¨cnf Â¬cnf Ï•) âŸ§ v        ~âŸ¨ âˆ¨cnf-sound (Â¬clause p) (Â¬cnf Ï•) v âŸ©
  (âŸ¦ Â¬clause p âŸ§ v) or (âŸ¦ Â¬cnf Ï• âŸ§ v)  ~âŸ¨ apÂ² _or_ (Â¬clause-sound p v) (Â¬cnf-sound Ï• v) âŸ©
  (not (âŸ¦ p âŸ§ v)) or (not (âŸ¦ Ï• âŸ§ v))   ~âŸ¨ not-and (âŸ¦ p âŸ§ v) _ âŸ¨
  not (âŸ¦ p âˆ· Ï• âŸ§ v)                    âˆ

-- naive algorithm

to-cnf : Prp n â†’ CNF n
to-cnf (atom x)  = cnf-atom x
to-cnf â€œâŠ¤â€       = âŠ¤cnf
to-cnf â€œâŠ¥â€       = âŠ¥cnf
to-cnf (p â€œâˆ§â€ q) = to-cnf p âˆ§cnf to-cnf q
to-cnf (p â€œâˆ¨â€ q) = to-cnf p âˆ¨cnf to-cnf q
to-cnf (â€œÂ¬â€ p)   = Â¬cnf (to-cnf p)

to-cnf-sound : (p : Prp n) (v : Val n)
             â†’ âŸ¦ to-cnf p âŸ§ v ï¼ âŸ¦ p âŸ§ v
to-cnf-sound (atom x)  v = cnf-atom-sound x v
to-cnf-sound  â€œâŠ¤â€      v = refl
to-cnf-sound  â€œâŠ¥â€      v = refl
to-cnf-sound (p â€œâˆ§â€ q) v =
    âˆ§cnf-sound (to-cnf p) (to-cnf q) v
  âˆ™ apÂ² _and_ (to-cnf-sound p v) (to-cnf-sound q v)
to-cnf-sound (p â€œâˆ¨â€ q) v =
    âˆ¨cnf-sound (to-cnf p) (to-cnf q) v
  âˆ™ apÂ² _or_ (to-cnf-sound p v) (to-cnf-sound q v)
to-cnf-sound (â€œÂ¬â€ p)   v =
    Â¬cnf-sound (to-cnf p) v
  âˆ™ ap not (to-cnf-sound p v)

-- quotation

quote-pos : Lit n â†’ Prp n
quote-pos (pos x) = atom x
quote-pos (neg x) = â€œÂ¬â€ (atom x)

quote-clause : Clause n â†’ Prp n
quote-clause []      = â€œâŠ¥â€
quote-clause (x âˆ· Ï•) = quote-pos x â€œâˆ¨â€ quote-clause Ï•

quote-cnf : CNF n â†’ Prp n
quote-cnf []       = â€œâŠ¤â€
quote-cnf (Ï• âˆ· Ïˆ) = quote-clause Ï• â€œâˆ§â€ quote-cnf Ïˆ

quote-pos-sound : (x : Lit n)
                â†’ (v : Val n) â†’ âŸ¦ x âŸ§ v ï¼ âŸ¦ quote-pos x âŸ§ v
quote-pos-sound (pos x) v = refl
quote-pos-sound (neg x) v = refl

quote-clause-sound : (Ï• : Clause n)
                   â†’ (v : Val n) â†’ âŸ¦ Ï• âŸ§ v ï¼ âŸ¦ quote-clause Ï• âŸ§ v
quote-clause-sound []      v = refl
quote-clause-sound (p âˆ· Ï•) v = apÂ² _or_ (quote-pos-sound p v) (quote-clause-sound Ï• v)

quote-cnf-sound : (Ïˆ : CNF n) â†’ (v : Val n) â†’ âŸ¦ Ïˆ âŸ§ v ï¼ âŸ¦ quote-cnf Ïˆ âŸ§ v
quote-cnf-sound []      v = refl
quote-cnf-sound (Ï• âˆ· Ïˆ) v = apÂ² _and_ (quote-clause-sound Ï• v) (quote-cnf-sound Ïˆ v)
